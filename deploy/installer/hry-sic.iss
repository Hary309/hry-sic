; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Smooth Interior Camera"
#define MyAppVersion "2.0.2"
#define MyAppPublisher "Piotr Krupa"
#define MyAppURL "https://hary309.github.io/projects/hry-plugins/hry-sic"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{EEB38E59-21FA-446B-9975-17F51BC9335E}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\h
DefaultGroupName={#MyAppName}
DisableDirPage=yes
DisableProgramGroupPage=yes

; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=.
OutputBaseFilename=hry-sic
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Types]
Name: "full"; Description: "Both games"
Name: "ets2"; Description: "Only Euro Truck Simulator 2";
Name: "ats"; Description: "Only American Truck Simulator";
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Components]
Name: "ets2"; Description: "Install for Euro Truck Simulator 2"; Types: full ets2
Name: "ats"; Description: "Install for American Truck Simulator"; Types: full ats

[Files]
Source: "..\..\vendor\hry-core\bin\release\hry-core.dll"; DestDir: "{code:GetETS2BinDir}\plugins"; Flags: ignoreversion; Check: CheckInstallETS2
Source: "..\..\build\Release\hry-sic.dll"; DestDir: "{code:GetETS2BinDir}\plugins\hry_plugins"; Flags: ignoreversion; Check: CheckInstallETS2
Source: "..\..\vendor\hry-core\assets\icons.ttf"; DestDir: "{code:GetETS2BinDir}\plugins\hry_assets"; Flags: ignoreversion; Check: CheckInstallETS2
Source: "..\..\vendor\hry-core\assets\OpenSans-Bold.ttf"; DestDir: "{code:GetETS2BinDir}\plugins\hry_assets"; Flags: ignoreversion; Check: CheckInstallETS2
Source: "..\..\vendor\hry-core\assets\OpenSans-Regular.ttf"; DestDir: "{code:GetETS2BinDir}\plugins\hry_assets"; Flags: ignoreversion; Check: CheckInstallETS2

Source: "..\..\vendor\hry-core\bin\release\hry-core.dll"; DestDir: "{code:GetATSBinDir}\plugins"; Flags: ignoreversion; Check: CheckInstallATS
Source: "..\..\build\Release\hry-sic.dll"; DestDir: "{code:GetATSBinDir}\plugins\hry_plugins"; Flags: ignoreversion; Check: CheckInstallATS
Source: "..\..\vendor\hry-core\assets\icons.ttf"; DestDir: "{code:GetATSBinDir}\plugins\hry_assets"; Flags: ignoreversion; Check: CheckInstallATS
Source: "..\..\vendor\hry-core\assets\OpenSans-Bold.ttf"; DestDir: "{code:GetATSBinDir}\plugins\hry_assets"; Flags: ignoreversion; Check: CheckInstallATS
Source: "..\..\vendor\hry-core\assets\OpenSans-Regular.ttf"; DestDir: "{code:GetATSBinDir}\plugins\hry_assets"; Flags: ignoreversion; Check: CheckInstallATS

[Icons]
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Code]
var
  ETS2DirPage: TInputDirWizardPage;
  ATSDirPage: TInputDirWizardPage;

  InstallETS2: Boolean;
  InstallATS: Boolean;

function GetETS2BinDir(Param: String): String;
begin
  Result := ETS2DirPage.Values[0] + '\bin\win_x64';
end;

function GetATSBinDir(Param: String): String;
begin
  Result := ATSDirPage.Values[0] + '\bin\win_x64';
end;

procedure InitializeWizard;
begin
  ATSDirPage := CreateInputDirPage(
    wpSelectComponents, 'Select American Truck Simulator main directory', 'Where American Truck Simulator is installed?', '', False, '');

  ATSDirPage.Add('ATS Main directory');
  ATSDirPage.Values[0] := GetPreviousData('ATSDirectory', '');
  
  ETS2DirPage := CreateInputDirPage(
    wpSelectComponents, 'Select Euro Truck Simulator 2 main directory', 'Where Euro Truck Simulator 2 is installed?', '', False, '');

  ETS2DirPage.Add('ETS 2 Main directory');
  ETS2DirPage.Values[0] := GetPreviousData('ETS2Directory', '');
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  Result := False;

  if PageID = ETS2DirPage.ID then
  begin
    InstallETS2 := WizardIsComponentSelected('ets2');
    Result := not InstallETS2;
  end;
  
  if PageID = ATSDirPage.ID then
  begin
    InstallATS := WizardIsComponentSelected('ats');
    Result := not InstallATS;
  end;
end;

procedure RegisterPreviousData(PreviousDataKey: Integer);
begin
  SetPreviousData(PreviousDataKey, 'ETS2Directory', ETS2DirPage.Values[0]);
  SetPreviousData(PreviousDataKey, 'ATSDirectory', ATSDirPage.Values[0]);
end;

function NextButtonClick(PageID: Integer): Boolean;
begin
    Result := True;

    if (PageID = ETS2DirPage.ID) and not FileExists(ExpandConstant(GetETS2BinDir('') + '\eurotrucks2.exe')) then begin
        Result := False;
        MsgBox('Cannot find bin/win_x64/eurotruck2.exe, make sure to select the MAIN directory of game', mbError, MB_OK);
        exit;
    end;

    if (PageID = ATSDirPage.ID) and not FileExists(ExpandConstant(GetATSBinDir('') + '\amtrucks.exe')) then begin
        Result := False;
        MsgBox('Cannot find bin/win_x64/amtrucks.exe, make sure to select the MAIN directory of game', mbError, MB_OK);
        exit;
    end;
end;

function CheckInstallETS2: Boolean;
begin
  Result := InstallETS2;
end;

function CheckInstallATS: Boolean;
begin
  Result := InstallATS;
end;